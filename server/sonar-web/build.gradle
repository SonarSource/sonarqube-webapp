sonarqube {
  properties {
    property "sonar.projectName", "${projectTitle} :: Web"
    property "sonar.sources", "src/main/js"
    property "sonar.tests", "src/main/js"
    property "sonar.test.inclusions", "src/main/js/**/__tests__/**"
    property "sonar.exclusions", "src/main/js/libs/third-party/**/*,src/main/js/libs/require.js,src/main/js/**/__tests__/**"
  }
}

apply plugin: 'com.moowork.node'

configurations {
  branding
}
dependencies {
  branding 'com.sonarsource:sonarsource-branding:1.3.0.307@war'
}

def webappDir = "${buildDir}/webapp"
def brandingDir = "${buildDir}/branding"

task unzipBranding(type: Copy) {
  from { zipTree(configurations.branding.singleFile) }
  into brandingDir
}
task copyBrandingSrc(type: Copy, dependsOn: unzipBranding) {
  into projectDir
  from file(brandingDir)
  include '**/*.js'
  includeEmptyDirs = false
}
task copyBrandingWebapp(type: Copy, dependsOn: unzipBranding) {
  into "${projectDir}/public"
  from file("${brandingDir}/src/main/webapp")
  include '**/*.svg'
  includeEmptyDirs = false
}
task copyBrandingFile(type: Copy, dependsOn: unzipBranding) {
  into "${projectDir}/public"
  from file(brandingDir)
  include '**/branding'
  includeEmptyDirs = false
}
task copyBranding() {
  dependsOn copyBrandingSrc
  dependsOn copyBrandingWebapp
  dependsOn copyBrandingFile
}

node {
  version = '8.10.0'
  yarnVersion = '1.5.1'
  download = true
}

yarn_run {
  if (release) { dependsOn copyBranding }
  inputs.dir('config')
  inputs.dir('public')
  inputs.dir('scripts')
  inputs.dir('src')
  inputs.file('.babelrc')
  inputs.file('build.gradle')
  inputs.file('package.json')
  inputs.file('tsconfig.json')
  inputs.file('yarn.lock')
  outputs.dir(webappDir)

  args = ['build']
}
